neuron izhikevich_tutorial:

  state:
    v mV = -65 mV    # Membrane potential in mV
    u real = 0.0
  end

  equations:
    v' = (.04 * v * v / mV + 5 * v + (140 - u) * mV + (I_e * GOhm)) / ms
    u' = a * (b * v / mV - u) / ms
  end

  parameters:
    a real = .02    # describes time scale of recovery variable
    b real = .2
    c mV = -65 mV
    d real = 8. 
  end

  input:
    spikes mV <- spike
    I_e pA <- continuous
  end

  output: spike

  update:
    integrate_odes()

    # add synaptic current
    v += spikes

    # Threshold crossing
    if v >= 30 mV:
       v = c
       u += d
       emit_spike()
    end

  end

end